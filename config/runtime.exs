import Config
require Logger

database_adapter = Application.get_env(:pinchflat, :database_adapter, :sqlite)

# config/runtime.exs is executed for all environments, including
# during releases. It is executed after compilation and before the
# system starts, so it is typically used to load production configuration
# and secrets from environment variables or elsewhere. Do not define
# any compile-time configuration in here, as it won't be applied.
# The block below contains prod specific runtime configuration.

# ## Using releases
#
# If you use `mix release`, you need to explicitly enable the server
# by passing the PHX_SERVER=true when you start it:
#
#     PHX_SERVER=true bin/pinchflat start
#
# Alternatively, you can use `mix phx.gen.release` to generate a `bin/server`
# script that automatically sets the env var above.
if System.get_env("PHX_SERVER") do
  config :pinchflat, PinchflatWeb.Endpoint, server: true
end

pot_provider_url =
  case System.get_env("POT_PROVIDER_URL") do
    value when is_binary(value) ->
      case String.trim(value) do
        "" -> nil
        trimmed -> trimmed
      end

    _ ->
      nil
  end

download_staging_directory =
  case System.get_env("DOWNLOAD_STAGING_PATH") do
    value when is_binary(value) ->
      case String.trim(value) do
        "" -> nil
        trimmed -> trimmed
      end

    _ ->
      nil
  end

config :pinchflat,
  basic_auth_username: System.get_env("BASIC_AUTH_USERNAME"),
  basic_auth_password: System.get_env("BASIC_AUTH_PASSWORD"),
  # Optional bgutil PO-token provider. An absent or blank URL keeps the
  # provider disabled and preserves the existing yt-dlp command line.
  po_token_provider_url: pot_provider_url,
  # Optional local download staging. An absent or blank path preserves direct
  # writes to the configured media directory.
  download_staging_directory: download_staging_directory

# Optional OIDC/OAuth2 single sign-on. When all three of these are set,
# the web UI requires logging in via the provider and BASIC_AUTH_* is
# ignored for browser routes (feeds keep basic auth for podcast clients).
oidc_issuer = System.get_env("OIDC_ISSUER")
oidc_client_id = System.get_env("OIDC_CLIENT_ID")
oidc_client_secret = System.get_env("OIDC_CLIENT_SECRET")
oidc_scopes = System.get_env("OIDC_SCOPES")
oidc_client_auth_method = System.get_env("OIDC_CLIENT_AUTH_METHOD")
oidc_provider_name = System.get_env("OIDC_PROVIDER_NAME")
oidc_redirect_uri = System.get_env("OIDC_REDIRECT_URI")

oidc_variables = [
  {"OIDC_ISSUER", oidc_issuer},
  {"OIDC_CLIENT_ID", oidc_client_id},
  {"OIDC_CLIENT_SECRET", oidc_client_secret},
  {"OIDC_SCOPES", oidc_scopes},
  {"OIDC_CLIENT_AUTH_METHOD", oidc_client_auth_method},
  {"OIDC_PROVIDER_NAME", oidc_provider_name},
  {"OIDC_REDIRECT_URI", oidc_redirect_uri}
]

oidc_value_set? = fn value -> is_binary(value) && value != "" end
oidc_configured? = Enum.any?(oidc_variables, fn {_name, value} -> oidc_value_set?.(value) end)

missing_oidc_variables =
  oidc_variables
  |> Enum.take(3)
  |> Enum.reject(fn {_name, value} -> oidc_value_set?.(value) end)
  |> Enum.map_join(", ", &elem(&1, 0))

cond do
  not oidc_configured? ->
    :ok

  missing_oidc_variables != "" ->
    raise """
    OIDC configuration is incomplete. Missing: #{missing_oidc_variables}.
    Set all required OIDC variables, or remove all OIDC_* variables to disable SSO.
    """

  true ->
    config :pinchflat, :oidc,
      issuer: oidc_issuer,
      client_id: oidc_client_id,
      client_secret: oidc_client_secret,
      scopes: oidc_scopes || "openid email profile",
      client_authentication_method: oidc_client_auth_method || "client_secret_basic",
      provider_name: oidc_provider_name || "Single Sign-On",
      redirect_uri: oidc_redirect_uri
end

arch_string = to_string(:erlang.system_info(:system_architecture))

system_arch =
  cond do
    String.contains?(arch_string, "arm") -> "arm"
    String.contains?(arch_string, "aarch") -> "arm"
    String.contains?(arch_string, "x86") -> "x86"
    true -> "unknown"
  end

if database_adapter == :sqlite do
  config :pinchflat, Pinchflat.Repo,
    load_extensions: [
      Path.join([:code.priv_dir(:pinchflat), "repo", "extensions", "sqlean-linux-#{system_arch}", "sqlean"])
    ]
end

# Some users may want to increase the number of workers that use yt-dlp to improve speeds
# Others may want to decrease the number of these workers to lessen the chance of an IP ban.
# Downloads can be tuned separately from indexing so slow media fetches don't starve indexing.
# These env vars set the boot-time Oban limits and override Settings whenever they
# are present. Omit them from Compose to control workers in the UI.
{yt_dlp_worker_count, _} = Integer.parse(System.get_env("YT_DLP_WORKER_CONCURRENCY", "5"))

{yt_dlp_download_worker_count, _} =
  Integer.parse(System.get_env("YT_DLP_DOWNLOAD_WORKER_CONCURRENCY", Integer.to_string(yt_dlp_worker_count)))

{yt_dlp_index_worker_count, _} =
  Integer.parse(System.get_env("YT_DLP_INDEX_WORKER_CONCURRENCY", Integer.to_string(yt_dlp_worker_count)))

{yt_dlp_remote_metadata_worker_count, _} =
  Integer.parse(System.get_env("YT_DLP_REMOTE_METADATA_WORKER_CONCURRENCY", Integer.to_string(yt_dlp_worker_count)))

# Reconcile applies network-bound backfills (thumbnails/subtitles) in parallel;
# tie its ceiling to the same politeness knob as the yt-dlp queues so a big
# online/full reconcile doesn't hammer YouTube any harder than normal downloads
config :pinchflat, reconcile_backfill_concurrency: max(yt_dlp_worker_count, 1)
# Used to set the cron for the yt-dlp update worker. The reason for this is
# to avoid all instances of PF updating yt-dlp at the same time, which 1)
# could result in rate limiting and 2) gives me time to react if an update
# breaks something
%{hour: current_hour, minute: current_minute} = DateTime.utc_now()

cron_jobs = [
  {"#{current_minute} #{current_hour} * * *", Pinchflat.YtDlp.UpdateWorker},
  {"0 1 * * *", Pinchflat.Downloading.MediaRetentionWorker},
  {"0 2 * * *", Pinchflat.Downloading.MediaQualityUpgradeWorker},
  # Discovery is opt-in in the database. The worker cancels this cheap
  # scheduled job while disabled, so a setting change does not require a
  # runtime config reload or a scheduler restart.
  {"0 3 * * *", Pinchflat.Discovery.Worker}
]

cron_jobs =
  if database_adapter == :sqlite do
    # Monthly, after retention (1AM) and quality upgrades (2AM) have had a
    # chance to delete records whose space the VACUUM can then reclaim.
    cron_jobs ++ [{"0 3 1 * *", Pinchflat.Diagnostics.DatabaseMaintenanceWorker}]
  else
    cron_jobs
  end

config :pinchflat, Oban,
  queues: [
    default: 10,
    fast_indexing: yt_dlp_index_worker_count,
    media_collection_indexing: yt_dlp_index_worker_count,
    media_fetching: yt_dlp_download_worker_count,
    remote_metadata: yt_dlp_remote_metadata_worker_count,
    local_data: 8,
    # Reconciliation and database compaction both reserve a full quiet window.
    # Serializing them here prevents each from waiting for the other to finish.
    maintenance: 1
  ],
  plugins: [
    # Keep old jobs for 30 days for display in the UI
    {Oban.Plugins.Pruner, max_age: 30 * 24 * 60 * 60},
    # Rescue orphaned jobs stuck in "executing" state after crash/restart
    {Oban.Plugins.Lifeline, rescue_after: :timer.minutes(30)},
    {Oban.Plugins.Cron, crontab: cron_jobs}
  ]

if config_env() == :prod do
  # Various paths. These ones shouldn't be tweaked if running in Docker
  media_path = System.get_env("MEDIA_PATH", "/downloads")
  config_path = System.get_env("CONFIG_PATH", "/config")
  log_path = System.get_env("LOG_PATH", Path.join([config_path, "logs", "pinchflat.log"]))
  metadata_path = System.get_env("METADATA_PATH", Path.join([config_path, "metadata"]))
  extras_path = System.get_env("EXTRAS_PATH", Path.join([config_path, "extras"]))
  postgres_backup_path = System.get_env("POSTGRES_BACKUP_PATH", Path.join([extras_path, "backups"]))
  tmpfile_path = System.get_env("TMPFILE_PATH", Path.join([System.tmp_dir!(), "pinchflat", "data"]))
  # This one can be changed if you want
  tz_data_path = System.get_env("TZ_DATA_PATH", Path.join([extras_path, "elixir_tz_data"]))
  # For running PF as a podcast host on self-hosted environments
  expose_feed_endpoints = String.length(System.get_env("EXPOSE_FEED_ENDPOINTS", "")) > 0
  # For running PF in a subdirectory via a reverse proxy
  base_route_path = System.get_env("BASE_ROUTE_PATH", "/")
  enable_ipv6 = String.length(System.get_env("ENABLE_IPV6", "")) > 0
  enable_prometheus = String.length(System.get_env("ENABLE_PROMETHEUS", "")) > 0

  config :logger, level: String.to_existing_atom(System.get_env("LOG_LEVEL", "debug"))

  config :pinchflat,
    yt_dlp_executable: System.find_executable("yt-dlp"),
    apprise_executable: System.find_executable("apprise"),
    media_directory: media_path,
    metadata_directory: metadata_path,
    extras_directory: extras_path,
    postgres_backup_directory: postgres_backup_path,
    tmpfile_directory: tmpfile_path,
    dns_cluster_query: System.get_env("DNS_CLUSTER_QUERY"),
    expose_feed_endpoints: expose_feed_endpoints,
    # This is configured in application.ex
    timezone: "UTC",
    log_path: log_path,
    base_route_path: base_route_path

  config :tzdata, :data_dir, tz_data_path

  {db_pool_size, _} = Integer.parse(System.get_env("DATABASE_POOL_SIZE", "10"))

  # Optional override for how long (ms) a query may hold a connection before it is
  # cancelled. Unset keeps the adapter defaults (45s for SQLite via config.exs, Ecto's
  # 15s for PostgreSQL). Accepted range is 15000-300000 (15s-5min); values outside
  # it are ignored and the defaults apply. Setting this high can tie up connections
  # longer, delay other queries and make the UI feel stuck while a slow query runs.
  db_timeout_opts =
    case System.get_env("DATABASE_TIMEOUT_MS", "") |> String.trim() |> Integer.parse() do
      {ms, ""} when ms >= 15_000 and ms <= 300_000 -> [timeout: ms]
      _ -> []
    end

  case database_adapter do
    :sqlite ->
      db_path = System.get_env("DATABASE_PATH", Path.join([config_path, "db", "pinchflat.db"]))
      # For testing alternate journal modes (see issue #137)
      journal_mode = String.to_existing_atom(System.get_env("JOURNAL_MODE", "wal"))

      # WAL lets readers run concurrently, so a larger pool mainly buys headroom
      # for the web UI and other jobs while a long operation holds connections.
      config :pinchflat,
             Pinchflat.Repo,
             [database: db_path, journal_mode: journal_mode, pool_size: db_pool_size] ++ db_timeout_opts

    :postgres ->
      database_url =
        System.get_env("DATABASE_URL") ||
          raise "DATABASE_URL is required by the PostgreSQL image"

      config :pinchflat,
             Pinchflat.Repo,
             [
               url: database_url,
               pool_size: db_pool_size,
               socket_options: if(enable_ipv6, do: [:inet6], else: [])
             ] ++ db_timeout_opts
  end

  config :pinchflat, Pinchflat.PromEx, disabled: !enable_prometheus

  # The secret key base is used to sign/encrypt cookies and other secrets.
  # A default value is used in config/dev.exs and config/test.exs but you
  # want to use a different value for prod and you most likely don't want
  # to check this value into version control, so we use an environment
  # variable instead.
  secret_key_base =
    if System.get_env("SECRET_KEY_BASE") do
      System.get_env("SECRET_KEY_BASE")
    else
      if System.get_env("RUN_CONTEXT") == "selfhosted" do
        # Using the default SECRET_KEY_BASE in a conventional production environment
        # is dangerous. Please set the SECRET_KEY_BASE environment variable if you're
        # deploying this to an internet-facing server. If you're running this in a
        # private network, it's likely safe to use the default value. If you want
        # to be extra safe, run `mix phx.gen.secret` and set the SECRET_KEY_BASE
        # environment variable to the output of that command.

        "ZkuQMStdmUzBv+gO3m3XZrtQW76e+AX3QIgTLajw3b/HkTLMEx+DOXr2WZsSS+n8"
      else
        raise """
        environment variable SECRET_KEY_BASE is missing.
        You can generate one by calling: mix phx.gen.secret
        """
      end
    end

  config :pinchflat, PinchflatWeb.Endpoint,
    http: [
      # Enable IPv6 and bind on all interfaces.
      # Set it to  {0, 0, 0, 0, 0, 0, 0, 1} for local network only access.
      # See the documentation on https://hexdocs.pm/plug_cowboy/Plug.Cowboy.html
      # for details about using IPv6 vs IPv4 and loopback vs public addresses.
      ip: if(enable_ipv6, do: {0, 0, 0, 0, 0, 0, 0, 0}, else: {0, 0, 0, 0}),
      port: String.to_integer(System.get_env("PORT") || "4000")
    ],
    url: [path: base_route_path],
    secret_key_base: secret_key_base

  config :pinchflat, :logger, [
    {:handler, :file_log, :logger_std_h,
     %{
       config: %{
         type: :file,
         file: String.to_charlist(log_path),
         filesync_repeat_interval: 5000,
         file_check: 5000,
         max_no_files: 5,
         max_no_bytes: 10_000_000
       },
       # Match the console formatter: render timestamps in the configured
       # timezone (see Pinchflat.LoggerFormatter), not OS local time.
       formatter:
         Logger.Formatter.new(
           format: {Pinchflat.LoggerFormatter, :format},
           metadata: [:request_id],
           utc_log: true
         )
     }}
  ]
end
